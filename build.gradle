plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.103'
    id "com.modrinth.minotaur" version "2.+"
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name "cursemaven"
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url "https://maven.latvian.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    }
    maven {
        url "https://jitpack.io"
        content {
            includeGroup "com.github.rtyley"
        }
    }
    maven {
        url "https://maven.wispforest.io/releases"
        content {
            includeGroup "io.wispforest"
            includeGroup "io.wispforest.endec"
        }
    }
    maven {
        url "https://maven.su5ed.dev/releases"
        content {
            includeGroup "org.sinytra"
            includeGroup "org.sinytra.forgified-fabric-api"
        }
    }
    maven {
        url "https://code.redspace.io/releases"
        content {
            includeGroup "io.redspace"
        }
    }
    maven {
        url "https://maven.blamejared.com"
        content {
            includeGroup "vazkii.patchouli"
            includeGroup "mezz.jei"
        }
    }
    maven {
        url "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
        content {
            includeGroup("software.bernie.geckolib")
        }
    }
    maven {
        url "https://maven.theillusivec4.top"
        content {
            includeGroup("top.theillusivec4.curios")
        }
    }
    maven {
        url 'https://maven.blamejared.com'
        content {
            includeGroup("com.hollingsworth.ars_nouveau")
            includeGroup("com.hollingsworth.nuggets")
        }
    }
    maven {
        url "https://maven.minecraftforge.net/"
        content {
            includeGroup("com.github.glitchfiend")
        }
    }
}

base {
    archivesName = "$mod_id-$minecraft_version"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers.add('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            client()

            programArguments.addAll '--width', '1920', '--height', '1080'

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id

            systemProperty 'mixin.debug.export', 'true'
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // gameDirectory = project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // mostly optional in a single mod project
        // but multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    // curios and accessories compat layer
    compileOnly("io.wispforest:accessories-neoforge:${accessories_version}+${minecraft_version}")
    compileOnly("maven.modrinth:accessories-compat-layer:${accessories_compat_layer_version}")
    implementation("maven.modrinth:curios:${curios_version}")
    // animation library
    implementation("software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}")

    // Jade
    implementation("maven.modrinth:jade:${jade_version}")
    // Touhou little maid
    implementation("curse.maven:touhou-little-maid-355044:${tlm_curse_version}") {
        exclude group: "maven.modrinth", module: "sodium"
        exclude group: "maven.modrinth", module: "embeddium"
    }
    // 铁魔法
    implementation("io.redspace:irons_spellbooks:${minecraft_version}-${irons_spells_version}") {
        exclude group: "top.theillusivec4.curios", module: "curios-neoforge"
    }

    // 铁魔法附属
    // Cataclysm: Spellbooks
    implementation("curse.maven:cataclysm-spellbooks-1099461:${cataclysm_spellbooks_version}")
    // Magic From The East
    implementation("maven.modrinth:magic-from-the-east:${magic_from_the_east_version}")
    // SnackPirate's Aeromancy Additions
    implementation("maven.modrinth:aeromancy-additions:${aeromancy_additions_version}")
    // Hazen 'N Stuff
    // implementation("curse.maven:hazen-n-stuff-1231627:${hazen_n_stuff_version}")
    // Discerning The Eldritch
    implementation("curse.maven:discerning-the-eldritch-1143065:${discerning_the_eldritch_version}")
    implementation("curse.maven:aces-spell-utils-1299492:${aces_spell_utils_version}")
    // Fire's Ender Expansion
    implementation("curse.maven:fires-ender-expansion-1245989:${fires_ender_expansion_version}")
    // Atlas' Additions
    implementation("maven.modrinth:atlas-additions:${atlas_additions_version}-${minecraft_version}")
    // Ender's Spells and Stuff: Requiem
    implementation("curse.maven:enders-spells-and-stuff-requiem-1336977:${enders_spells_and_stuff_requiem_version}")
    // Dreamless Spells and Spellbooks
    implementation("curse.maven:dreamless-spells-1250250:${dreamless_spells_version}")
    // Majo's Spell Enchantment
    implementation("curse.maven:majo-spell-enchantment-1318486:${majo_spell_enchantment_version}")
    // Snow Waifu Spell
    implementation("curse.maven:snow-waifu-spell-1320652:${snow_waifu_spell_version}")
    implementation("curse.maven:the-twilight-forest-227639:${twilight_forest_curse_version}")
    // GTBC's Geomancy Plus - Iron's Spells Addon
    implementation("curse.maven:gtbcs-geomancy-plus-1352485:${gtbcs_geomancy_plus_version}")
    implementation("curse.maven:gtbcs-spelllib-1194714:${gtbcs_spelllib_version}")


    // 新生魔艺
    implementation("com.hollingsworth.ars_nouveau:ars_nouveau-${minecraft_version}:${ars_nouveau_version}")

    // 工具和前置
    // 灾变
    implementation("maven.modrinth:l_enders-cataclysm:${l_enders_cataclysm_version}-${minecraft_version}")
    implementation("maven.modrinth:azurelib:${azurelib_version}")
    runtimeOnly("vazkii.patchouli:Patchouli:${patchouli_version}")
    runtimeOnly("maven.modrinth:appleskin:${appleskin_version}")
    runtimeOnly("maven.modrinth:playeranimator:${playeranimator_version}")
    runtimeOnly("maven.modrinth:lionfish-api:${lionfish_version}-neoforge")
    runtimeOnly("curse.maven:expandability-465066:${expandability_version}")
    runtimeOnly("curse.maven:mowzies-mobs-250498:${mowzies_mobs_version}")
    runtimeOnly("curse.maven:placebo-283644:6926281")
    runtimeOnly("curse.maven:apothic-attributes-898963:6751650")
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version               : minecraft_version,
            minecraft_version_range         : minecraft_version_range,
            neo_version                     : neo_version,
            neo_version_range               : neo_version_range,
            loader_version_range            : loader_version_range,
            mod_id                          : mod_id,
            mod_name                        : mod_name,
            mod_license                     : mod_license,
            mod_version                     : mod_version,
            mod_authors                     : mod_authors,
            mod_description                 : mod_description,
            irons_spells_version_range      : irons_spells_version_range,
            touhou_little_maid_version_range: touhou_little_maid_version_range
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

createMinecraftArtifacts {
    dependsOn generateModMetadata
}

// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = mod_id
    // versionNumber = "1.0.0"
    versionType = "beta"
    uploadFile = jar
    gameVersions = ["1.21.1"]
    loaders = ["neoforge"]
    dependencies {
        required.project "touhou-little-maid"
        required.project "irons-spells-n-spellbooks"
    }
}